proc celldef() {
  topol()
  subsets()
  geom()
  biophys()
  // geom_nseg()
}

create soma, ABD, axonstart, AIS, axon, axoD, axoD_sec[4]
create nABD[3], nABD_sec[4]

proc topol() { local i
  connect ABD(0), soma(1)
  connect axonstart(0), ABD(1)
  connect AIS(0), axonstart(1)
  connect axon(0), AIS(1)
  connect axoD(0), ABD(1)
  for i = 0, 1 connect axoD_sec[i](0), axoD(1)
  for i = 2, 3 connect axoD_sec[i](0), axoD_sec[1](1)
  for i = 0, 2 connect nABD[i](0), soma(0)
  for i = 0, 1 connect nABD_sec[i](0), nABD(1)
  for i = 2, 3 connect nABD_sec[i](0), nABD[1](1)
  basic_shape()
}
proc basic_shape() {
  soma {pt3dclear() pt3dadd(0, 0, 0, 1) pt3dadd(15, 0, 0, 1)}
  ABD {pt3dclear() pt3dadd(15, 0, 0, 1) pt3dadd(45, 0, 0, 1)}
  axonstart {pt3dclear() pt3dadd(45, 0, 0, 1) pt3dadd(45, 30, 0, 1)}
  AIS {pt3dclear() pt3dadd(45, 30, 0, 1) pt3dadd(45, 60, 0, 1)}
  axon {pt3dclear() pt3dadd(45, 60, 0, 1) pt3dadd(45, 105, 0, 1)}
  axoD {pt3dclear() pt3dadd(45, 0, 0, 1) pt3dadd(75, 0, 0, 1)}
  axoD_sec {pt3dclear() pt3dadd(75, 0, 0, 1) pt3dadd(90, 30, 0, 1)}
  axoD_sec[1] {pt3dclear() pt3dadd(75, 0, 0, 1) pt3dadd(105, -14, 0, 1)}
  axoD_sec[2] {pt3dclear() pt3dadd(105, -14, 0, 1) pt3dadd(135, 0, 0, 1)}
  axoD_sec[3] {pt3dclear() pt3dadd(105, -14, 0, 1) pt3dadd(120, -44, 0, 1)}
  nABD {pt3dclear() pt3dadd(0, 0, 0, 1) pt3dadd(0, 30, 0, 1)}
  nABD[1] {pt3dclear() pt3dadd(0, 0, 0, 1) pt3dadd(-29, 0, 0, 1)}
  nABD[2] {pt3dclear() pt3dadd(0, 0, 0, 1) pt3dadd(0, -29, 0, 1)}
  nABD_sec {pt3dclear() pt3dadd(0, 30, 0, 1) pt3dadd(-14, 60, 0, 1)}
  nABD_sec[1] {pt3dclear() pt3dadd(0, 30, 0, 1) pt3dadd(15, 60, 0, 1)}
  nABD_sec[2] {pt3dclear() pt3dadd(-29, 0, 0, 1) pt3dadd(-59, 15, 0, 1)}
  nABD_sec[3] {pt3dclear() pt3dadd(-29, 0, 0, 1) pt3dadd(-59, -14, 0, 1)}
}

objref all, all_biophys, all_ABD
proc subsets() { local i
  objref all, all_biophys, all_ABD
  all = new SectionList()
    soma all.append()
    ABD all.append()
    axonstart all.append()
    AIS all.append()
    axon all.append()
    axoD all.append()
    for i=0, 3 axoD_sec[i] all.append()
    for i=0, 2 nABD[i] all.append()
    for i=0, 3 nABD_sec[i] all.append()

  all_biophys = new SectionList()
    ABD all_biophys.append()
    axonstart all_biophys.append()
    axoD all_biophys.append()
    for i=0, 3 axoD_sec[i] all_biophys.append()
    for i=0, 2 nABD[i] all_biophys.append()
    for i=0, 3 nABD_sec[i] all_biophys.append()

      all_ABD=new SectionList()
     ABD all_ABD.append()
    axonstart all_ABD.append()
    axoD all_ABD.append()
    for i=0, 3 axoD_sec[i] all_ABD.append()

}
proc geom() {
  
   soma {  L = 25      }
  ABD {  L = 7       }
  axonstart {  L = 17.2       }
  AIS {  L = 32.7       }
  axon {  L = 800      }
  axoD {  L = 4       }
  axoD_sec {  L = 9       }
  axoD_sec[1] {  L = 291       }
  axoD_sec[2] {  L = 37       }
  axoD_sec[3] {  L = 49       }
  nABD {  L = 22       }
  nABD[1] {  L = 85       }
  nABD[2] {  L = 356       }
  nABD_sec {  L = 504       }
  nABD_sec[1] {  L = 442       }
  nABD_sec[2] {  L = 365       }
  nABD_sec[3] {  L = 139       }
   
   
// nseg definition has to be launched before diam definition for tapering to be taken into account

   forsec all { nseg = int((L/(0.1*lambda_f(1000))+.999)/2)*2 + 1  }
   
  soma {    diam = 10  }
  ABD {   diam(0:1) = 2.7:2.3  }
  axonstart {    diam(0:1) =1.26:1.1  }
  AIS {       diam(0:1) = 1.1:0.6  }
  axon {     diam = 0.6 }
  axoD {      diam(0:1) = 2.3:2  }
  axoD_sec {      diam = 0.8  }
  axoD_sec[1] {      diam(0:1) = 1.5:1  }
  axoD_sec[2] {     diam = 0.8  }
  axoD_sec[3] {     diam = 0.8  }
  nABD {  diam(0:1) = 3.5:3  }
  nABD[1] {     diam(0:1) = 2:1.8  }
  nABD[2] {      diam(0:1) = 2.5:0.8 }
  nABD_sec {      diam(0:1) = 1.5:0.8 }
  nABD_sec[1] {      diam(0:1) = 1.5:0.8 }
  nABD_sec[2] {      diam(0:1) = 1:0.8  }
  nABD_sec[3] {      diam(0:1) = 1:0.8  }
}

proc biophys() {
  forsec all {
    Ra = 150
    cm = 0.75
    insert pasnts
      g_pasnts = 1e-05
      e_pasnts = -50
  }
  forsec all_biophys {
    insert CAV13
      gbar_CAV13 = 1.25
      iLCa_CAV13 = 0
    insert Ih
      gbar_Ih = 3
    insert kaDa
      gbar_kaDa = 50
      taurecov_kaDa = 25
    insert kdrDA
      gbar_kdrDA = 150
    insert Na12
      gbar_Na12 = 75
    insert cad
    insert kca
      gbar_kca = 0.1
        
  }
  soma {
    insert CAV13
      gbar_CAV13 = 1.25
      iLCa_CAV13 = 0
    insert Ih
      gbar_Ih = 3
    insert kaDasoma
      gbar_kaDasoma = 150
      taurecov_kaDasoma = 25
    insert kdrDA
      gbar_kdrDA = 150
    insert Na12
      gbar_Na12 = 75
    insert cad
    insert kca
      gbar_kca = 0.1
        
  }
  AIS {
    insert kdrDA
      gbar_kdrDA = 4000
    insert Na12
      gbar_Na12 = 4000
  }
  axon {
    insert kdrDA
      gbar_kdrDA = 400
    insert Na12
      gbar_Na12 = 400
  }
  forsec all { ek=-90
ena=60  }
  
}
access soma

celldef()
