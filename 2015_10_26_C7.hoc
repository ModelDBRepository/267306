proc celldef() {
  topol()
  subsets()
  geom()
  biophys()
  // geom_nseg()
}

create soma, ABD[2], axonstart, AIS, axon, axoD, axoD_sec[4]
create interD, nABD[3], nABD_sec[4]

proc topol() { local i
  connect ABD(0), soma(1)
  connect ABD[1](0), ABD(1)
  connect axonstart(0), ABD[1](1)
  connect AIS(0), axonstart(1)
  connect axon(0), AIS(1)
  connect axoD(0), ABD[1](1)
  for i = 0, 1 connect axoD_sec[i](0), axoD(1)
  for i = 2, 3 connect axoD_sec[i](0), axoD_sec[1](1)
  connect interD(0), ABD(1)
  for i = 0, 2 connect nABD[i](0), soma(0)
  for i = 0, 1 connect nABD_sec[i](0), nABD(1)
  for i = 2, 3 connect nABD_sec[i](0), nABD_sec[1](1)
  basic_shape()
}
proc basic_shape() {
  soma {pt3dclear() pt3dadd(0, 0, 0, 1) pt3dadd(15, 0, 0, 1)}
  ABD {pt3dclear() pt3dadd(15, 0, 0, 1) pt3dadd(30, 30, 0, 1)}
  ABD[1] {pt3dclear() pt3dadd(30, 30, 0, 1) pt3dadd(15, 45, 0, 1)}
  axonstart {pt3dclear() pt3dadd(15, 45, 0, 1) pt3dadd(45, 60, 0, 1)}
  AIS {pt3dclear() pt3dadd(45, 60, 0, 1) pt3dadd(75, 75, 0, 1)}
  axon {pt3dclear() pt3dadd(75, 75, 0, 1) pt3dadd(135, 105, 0, 1)}
  axoD {pt3dclear() pt3dadd(15, 45, 0, 1) pt3dadd(0, 75, 0, 1)}
  axoD_sec {pt3dclear() pt3dadd(0, 75, 0, 1) pt3dadd(-29, 90, 0, 1)}
  axoD_sec[1] {pt3dclear() pt3dadd(0, 75, 0, 1) pt3dadd(15, 105, 0, 1)}
  axoD_sec[2] {pt3dclear() pt3dadd(15, 105, 0, 1) pt3dadd(0, 135, 0, 1)}
  axoD_sec[3] {pt3dclear() pt3dadd(15, 105, 0, 1) pt3dadd(45, 120, 0, 1)}
  interD {pt3dclear() pt3dadd(30, 30, 0, 1) pt3dadd(75, 45, 0, 1)}
  nABD {pt3dclear() pt3dadd(0, 0, 0, 1) pt3dadd(-29, 30, 0, 1)}
  nABD[1] {pt3dclear() pt3dadd(0, 0, 0, 1) pt3dadd(-29, -29, 0, 1)}
  nABD[2] {pt3dclear() pt3dadd(0, 0, 0, 1) pt3dadd(0, -44, 0, 1)}
  nABD_sec {pt3dclear() pt3dadd(-29, 30, 0, 1) pt3dadd(-29, 60, 0, 1)}
  nABD_sec[1] {pt3dclear() pt3dadd(-29, 30, 0, 1) pt3dadd(-59, 30, 0, 1)}
  nABD_sec[2] {pt3dclear() pt3dadd(-59, 30, 0, 1) pt3dadd(-74, 60, 0, 1)}
  nABD_sec[3] {pt3dclear() pt3dadd(-59, 30, 0, 1) pt3dadd(-89, 15, 0, 1)}
}

objref all, all_biophys, all_ABD

proc subsets() { local i
  objref all, all_biophys, all_ABD
  all = new SectionList()
    soma all.append()
    for i=0, 1 ABD[i] all.append()
    axonstart all.append()
    AIS all.append()
    axon all.append()
    axoD all.append()
    for i=0, 3 axoD_sec[i] all.append()
    interD all.append()
    for i=0, 2 nABD[i] all.append()
    for i=0, 3 nABD_sec[i] all.append()

  all_biophys = new SectionList()
    for i=0, 1 ABD[i] all_biophys.append()
    axonstart all_biophys.append()
    axoD all_biophys.append()
    for i=0, 3 axoD_sec[i] all_biophys.append()
    interD all_biophys.append()
    for i=0, 2 nABD[i] all_biophys.append()
    for i=0, 3 nABD_sec[i] all_biophys.append()

      all_ABD=new SectionList()
     for i=0, 1 ABD[i] all_ABD.append()
    axonstart all_ABD.append()
    axoD all_ABD.append()
    for i=0, 3 axoD_sec[i] all_ABD.append()
    interD all_ABD.append()


}
proc geom() {

  soma {  L = 32    }
  ABD {  L = 13     }
  ABD[1] {  L = 82     }
  axonstart {  L = 14     }
  AIS {  L = 37.5     }
  axon {  L = 800     }
  axoD {  L = 174     }
  axoD_sec {  L = 19     }
  axoD_sec[1] {  L = 14     }
  axoD_sec[2] {  L = 46     }
  axoD_sec[3] {  L = 62     }
  interD {  L = 158     }
  nABD {  L = 13     }
  nABD[1] {  L = 220     }
  nABD[2] {  L = 33     }
  nABD_sec {  L = 83     }
  nABD_sec[1] {  L = 85     }
  nABD_sec[2] {  L = 112     }
  nABD_sec[3] {  L = 168     }

// nseg definition has to be launched before diam definition for tapering to be taken into account

  forsec all { nseg = int((L/(0.1*lambda_f(1000))+.999)/2)*2 + 1  }
  
  soma {      diam = 10  }
  ABD {     diam(0:1) = 3.3:3  }
  ABD[1] {     diam(0:1) = 3:2.7  }
  axonstart {     diam(0:1) = 1.277:0.9  }
  AIS {       diam(0:1) = 0.9:0.7 }
  axon {      diam = 0.7  }
  axoD {      diam(0:1) = 1.4:1  }
  axoD_sec {     diam = 1  }
  axoD_sec[1] {     diam = 1  }
  axoD_sec[2] {     diam = 0.8 }
  axoD_sec[3] {     diam = 0.8  }
  interD { diam(0:1) = 1.2:0.8  }
  nABD {  diam(0:1) = 4:3  }
  nABD[1] {   diam(0:1) = 2.5:0.8  }
  nABD[2] {  diam(0:1) = 2.4:1.5  }
  nABD_sec {  diam(0:1) = 1:0.8  }
  nABD_sec[1] {  diam(0:1) = 2:1.5 }
  nABD_sec[2] {      diam(0:1) = 1:0.8  }
  nABD_sec[3] {     diam(0:1) = 1:0.8  }
}
proc biophys() {
  forsec all {
    Ra = 150
    cm = 0.75
    insert pasnts
      g_pasnts = 1e-05
      e_pasnts = -50
  }
  forsec all_biophys {
    insert CAV13
      gbar_CAV13 = 1.25
      iLCa_CAV13 = 0
    insert Ih
      gbar_Ih = 3
    insert kaDa
      gbar_kaDa = 50
      taurecov_kaDa = 25
    insert kdrDA
      gbar_kdrDA = 150
    insert Na12
      gbar_Na12 = 75
    insert cad
    insert kca
      gbar_kca = 0.1
        
  }
  soma {
    insert CAV13
      gbar_CAV13 = 1.25
      iLCa_CAV13 = 0
    insert Ih
      gbar_Ih = 3
    insert kaDasoma
      gbar_kaDasoma = 150
      taurecov_kaDasoma = 25
    insert kdrDA
      gbar_kdrDA = 150
    insert Na12
      gbar_Na12 = 75
    insert cad
    insert kca
      gbar_kca = 0.1
        
  }
  AIS {
    insert kdrDA
      gbar_kdrDA = 4000
    insert Na12
      gbar_Na12 = 4000
  }
  axon {
    insert kdrDA
      gbar_kdrDA = 400
    insert Na12
      gbar_Na12 = 400
  }
  forsec all { ek=-90
ena=60  }
  
}
access soma

celldef()
