proc celldef() {
  topol()
  subsets()
  geom()
  biophys()
  // geom_nseg()
}

create soma, ABD[2], interD, axoD, axonstart, AIS, axon
create nABD[3], nABD_sec[12]

proc topol() { local i
  connect ABD(0), soma(1)
  connect ABD[1](0), ABD(1)
  connect interD(0), ABD(1)
  connect axoD(0), ABD[1](1)
  connect axonstart(0), ABD[1](1)
  connect AIS(0), axonstart(1)
  connect axon(0), AIS(1)
  for i = 0, 2 connect nABD[i](0), soma(0)
  for i = 0, 1 connect nABD_sec[i](0), nABD(1)
  for i = 2, 3 connect nABD_sec[i](0), nABD_sec(1)
  for i = 4, 5 connect nABD_sec[i](0), nABD_sec[1](1)
  for i = 6, 7 connect nABD_sec[i](0), nABD[1](1)
  for i = 8, 9 connect nABD_sec[i](0), nABD_sec[7](1)
  for i = 10, 11 connect nABD_sec[i](0), nABD_sec[9](1)
  basic_shape()
}
proc basic_shape() {
  soma {pt3dclear() pt3dadd(0, 0, 0, 1) pt3dadd(15, 0, 0, 1)}
  ABD {pt3dclear() pt3dadd(15, 0, 0, 1) pt3dadd(45, 0, 0, 1)}
  ABD[1] {pt3dclear() pt3dadd(45, 0, 0, 1) pt3dadd(60, -14, 0, 1)}
  interD {pt3dclear() pt3dadd(45, 0, 0, 1) pt3dadd(60, 30, 0, 1)}
  axoD {pt3dclear() pt3dadd(60, -14, 0, 1) pt3dadd(105, -14, 0, 1)}
  axonstart {pt3dclear() pt3dadd(60, -14, 0, 1) pt3dadd(60, -44, 0, 1)}
  AIS {pt3dclear() pt3dadd(60, -44, 0, 1) pt3dadd(60, -74, 0, 1)}
  axon {pt3dclear() pt3dadd(60, -74, 0, 1) pt3dadd(60, -134, 0, 1)}
  nABD {pt3dclear() pt3dadd(0, 0, 0, 1) pt3dadd(-14, 75, 0, 1)}
  nABD[1] {pt3dclear() pt3dadd(0, 0, 0, 1) pt3dadd(-59, 0, 0, 1)}
  nABD[2] {pt3dclear() pt3dadd(0, 0, 0, 1) pt3dadd(-29, -59, 0, 1)}
  nABD_sec {pt3dclear() pt3dadd(-14, 75, 0, 1) pt3dadd(0, 90, 0, 1)}
  nABD_sec[1] {pt3dclear() pt3dadd(-14, 75, 0, 1) pt3dadd(-44, 90, 0, 1)}
  nABD_sec[2] {pt3dclear() pt3dadd(0, 90, 0, 1) pt3dadd(0, 120, 0, 1)}
  nABD_sec[3] {pt3dclear() pt3dadd(0, 90, 0, 1) pt3dadd(30, 90, 0, 1)}
  nABD_sec[4] {pt3dclear() pt3dadd(-44, 90, 0, 1) pt3dadd(-44, 120, 0, 1)}
  nABD_sec[5] {pt3dclear() pt3dadd(-44, 90, 0, 1) pt3dadd(-74, 90, 0, 1)}
  nABD_sec[6] {pt3dclear() pt3dadd(-59, 0, 0, 1) pt3dadd(-89, -29, 0, 1)}
  nABD_sec[7] {pt3dclear() pt3dadd(-59, 0, 0, 1) pt3dadd(-89, 15, 0, 1)}
  nABD_sec[8] {pt3dclear() pt3dadd(-89, 15, 0, 1) pt3dadd(-134, 15, 0, 1)}
  nABD_sec[9] {pt3dclear() pt3dadd(-89, 15, 0, 1) pt3dadd(-104, 45, 0, 1)}
  nABD_sec[10] {pt3dclear() pt3dadd(-104, 45, 0, 1) pt3dadd(-134, 45, 0, 1)}
  nABD_sec[11] {pt3dclear() pt3dadd(-104, 45, 0, 1) pt3dadd(-104, 75, 0, 1)}
}

objref all, all_biophys, all_ABD
proc subsets() { local i
  objref all, all_biophys, all_ABD
  all = new SectionList()
    soma all.append()
    for i=0, 1 ABD[i] all.append()
    interD all.append()
    axoD all.append()
    axonstart all.append()
    AIS all.append()
    axon all.append()
    for i=0, 2 nABD[i] all.append()
    for i=0, 11 nABD_sec[i] all.append()

  all_biophys = new SectionList()
    for i=0, 1 ABD[i] all_biophys.append()
    interD all_biophys.append()
    axoD all_biophys.append()
    axonstart all_biophys.append()
    for i=0, 2 nABD[i] all_biophys.append()
    for i=0, 11 nABD_sec[i] all_biophys.append()

      all_ABD= new SectionList()
    for i=0, 1 ABD[i] all_ABD.append()
    interD all_ABD.append()
    axoD all_ABD.append()
    axonstart all_ABD.append()

}
proc geom() {

  soma {  L = 19    }
  ABD {  L = 24     }
  ABD[1] {  L = 12     }
  interD {  L = 297     }
  axoD {  L = 6     }
  axonstart {  L = 23.4     }
  AIS {  L = 28.4     }
  axon {  L = 800     }
  nABD {  L = 88     }
  nABD[1] {  L = 67     }
  nABD[2] {  L = 381     }
  nABD_sec {  L = 20     }
  nABD_sec[1] {  L = 136     }
  nABD_sec[2] {  L = 353     }
  nABD_sec[3] {  L = 184     }
  nABD_sec[4] {  L = 5     }
  nABD_sec[5] {  L = 115     }
  nABD_sec[6] {  L = 117     }
  nABD_sec[7] {  L = 33     }
  nABD_sec[8] {  L = 8     }
  nABD_sec[9] {  L = 3     }
  nABD_sec[10] {  L = 72     }
  nABD_sec[11] {  L = 376     }

// nseg definition has to be launched before diam definition for tapering to be taken into account

  forsec all { nseg = int((L/(0.1*lambda_f(1000))+.999)/2)*2 + 1  }
  
soma {  diam = 10 }
  ABD {  diam(0:1) = 3.2: 3 }
  ABD[1] {  diam(0:1) = 3:2.8 }
  interD {   diam(0:1) = 2.1:0.8  }
  axoD {  diam(0:1) = 1.45:1.4 }
  axonstart {  diam(0:1) = 1.89: 1.35 }
  AIS {  diam(0:1) = 1.35:0.83  }
  axon {  diam = 0.83 }
  nABD {  diam(0:1) = 1.6:1.4 }
  nABD[1] {  diam(0:1) = 2:1.8  }
  nABD[2] {   diam(0:1) = 2.5:0.8  }
  nABD_sec {  diam = 1  }
  nABD_sec[1] {   diam = 1  }
  nABD_sec[2] {   diam = 0.8  }
  nABD_sec[3] {    diam = 0.8  }
  nABD_sec[4] {   diam = 0.8  }
  nABD_sec[5] {    diam = 0.8 }
  nABD_sec[6] {   diam(0:1) = 1:0.8  }
  nABD_sec[7] {  diam = 1  }
  nABD_sec[8] {  diam = 1  }
  nABD_sec[9] {  diam = 1  }
  nABD_sec[10] {  diam = 0.8  }
  nABD_sec[11] {   diam = 0.8  }
}

proc biophys() {
  forsec all {
    Ra = 150
    cm = 0.75
  }
  forsec all_biophys {
    insert pasnts
      g_pasnts = 1e-05
      e_pasnts = -50
    insert kca
      gbar_kca = 0.1
        
    insert cad
    insert Na12
      gbar_Na12 = 75
    insert kdrDA
      gbar_kdrDA = 150
    insert kaDa
      gbar_kaDa = 50
      taurecov_kaDa = 25
    insert Ih
      gbar_Ih = 3
    insert CAV13
      gbar_CAV13 = 1.25
      iLCa_CAV13 = 0
  }
  soma {
    insert pasnts
      g_pasnts = 1e-05
      e_pasnts = -50
    insert kca
      gbar_kca = 0.1
        
    insert cad
    insert Na12
      gbar_Na12 = 75
    insert kdrDA
      gbar_kdrDA = 150
    insert kaDasoma
      gbar_kaDasoma = 150
      taurecov_kaDasoma = 25
    insert Ih
      gbar_Ih = 3
    insert CAV13
      gbar_CAV13 = 1.25
      iLCa_CAV13 = 0
  }
  AIS {
    insert pasnts
      g_pasnts = 1e-05
      e_pasnts = -50
    insert Na12
      gbar_Na12 = 4000
    insert kdrDA
      gbar_kdrDA = 4000
  }
  axon {
    insert pasnts
      g_pasnts = 1e-05
      e_pasnts = -50
    insert Na12
      gbar_Na12 = 400
    insert kdrDA
      gbar_kdrDA = 400
  }
  forsec all { ek=-90
ena=60  }
}
access soma

celldef()
